apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
  compileSdkVersion targetSdk
  buildToolsVersion buildTools

  defaultConfig {
    applicationId "adeln.boilerplate"

    minSdkVersion minSdk
    targetSdkVersion targetSdk

    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
    androidTest.java.srcDirs += 'src/androidTest/kotlin'
  }

  kapt {
    generateStubs = true
  }
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
  compile supportDependencies.appCompat

  testCompile "junit:junit:4.12"
  testCompile "org.robolectric:robolectric:3.0"

  androidTestCompile(supportTestDependencies.rules) { exclude module: "support-annotations" }
  androidTestCompile(supportTestDependencies.espresso) { exclude module: "support-annotations" }
}

final adb = android.getAdbExe()
final installTask = "installDebug"
final testTask = "connectedDebugAndroidTest"

task grantAnimationPermission(type: Exec, dependsOn: installTask) {
  commandLine "$adb shell pm grant $android.defaultConfig.applicationId android.permission.SET_ANIMATION_SCALE".split(' ')
}

tasks.whenTaskAdded { task ->
  if (task.name.equals(testTask)) {
    task.dependsOn grantAnimationPermission
  }
}

task testUi(dependsOn: testTask)